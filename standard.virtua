;; Virtua standard language.                      -*- Scheme -*-
;; Copyright (c) 2012 Manuel Simoni. See license at end of file.

($define! define $define!)
(define def define)
(define begin $begin)
(define if $if)
(define #void #inert)
(define _ #ignore)
(define vau $vau)
(define loop $loop)

(define ' (vau (form) _ form))

;;;; These are adapted from John Shutt's SINK and the R-1RK.

(define list (wrap (vau x _ x)))

(define list*
   (wrap (vau args _
            (begin
               (define aux
                  (wrap (vau ((head . tail)) _
                           (if (null? tail)
                               head
                               (cons head (aux tail))))))
               (aux args)))))

(define vau
   ((wrap (vau (vau) _
             (vau (formals eformal . body) env
                (eval (list vau formals eformal
                            (cons begin body))
                      env))))
      vau))

(define lambda
   (vau (formals . body) env
      (wrap (eval (list* vau formals _ body)
                  env))))

(define caar (lambda (((x . _) . _)) x))
(define cdar (lambda (((_ . x) . _)) x))
(define cadr (lambda ((_ . (x . _))) x))
(define cddr (lambda ((_ . (_ . x))) x))

(define apply
   (lambda (appv arg . opt)
      (eval (cons (unwrap appv) arg)
            (if (null? opt)
                (make-environment)
                (car opt)))))

(define or?
   (lambda (a b) (if a #t (if b #t #f))))

(define map
   (lambda (fn list)
      (if (null? list)
          ()
          (cons (fn (car list))
                (map fn (cdr list))))))

(define let
   (vau (bindings . body) env
      (eval (cons (list* lambda (map car bindings) body)
                  (map cadr bindings))
            env)))

(define let*
   (vau (bindings . body) env
      (eval (if (null? bindings)
                (list* let bindings body)
                (list let
                      (list (car bindings))
                      (list* let* (cdr bindings) body)))
            env)))

(define cond
   (vau clauses env
      (define aux
         (lambda ((test . body) . clauses)
            (if (eval test env)
                (apply (wrap begin) body env)
                (apply (wrap cond) clauses env))))
      (if (null? clauses)
          #void
          (apply aux clauses))))

(define not? (lambda (x) (if x #f #t)))

(define when
   (vau (test . body) env
      (eval (list if test (list* begin body) #void) env)))

(define unless
   (vau (test . body) env
      (eval (list if test #void (list* begin body)) env)))

(define get-current-environment (wrap (vau () e e)))

(define *top-level-environment* (get-current-environment))

(define provide
   (vau (symbols . body) env
      (eval (list define symbols
               (list let ()
                  (list* begin body)
                  (list* list symbols)))
            env)))

;;;; Virtua-specific forms

(define assert
   (vau (that) env
      (if (not? (eval that env))
          (error (to-string that))
          #void)))

(define catch
   (vau (tag . body) env
      (eval (list $catch tag (list* begin body)) env)))

(define block
   (vau (name . body) env
      ((eval (list lambda (list name)
                (list* catch name body))
             env)
       (cons () ()))))

(define return-from
   (lambda (name . value)
      (throw name (if (null? value) #void (car value)))))

(define unwind-protect
   (vau (protected . cleanup) env
      (eval (list $unwind-protect protected (list* begin cleanup)) env)))

(define scope
   (vau body env
      (eval (list* let () body) env)))

(define set!
   (vau (place value) env
      (if (instance? place Pair)
          (let (((cmb . args) place))
             (eval (list* (setter (eval cmb env)) value args) env))
          (eval (list $set! place value) env))))

(define setter (lambda (obj) (get-slot obj "setter")))
(define set-setter! (lambda (obj setter) (set-slot! obj "setter" setter)))

;;;; Object System

(provide (define-class
          define-interface
          define-implementation
          define-generic
          define-method)

   (define define-class
      (vau (name . stuff) env
         (if (null? stuff)
             (eval (list define name (make-class ())) env)
             (let (((superclasses . stuff) stuff))
                (let ((c (make-class (map (lambda (sc) (eval sc env)) superclasses))))
                   (eval (list define name c) env)
                   (unless (null? stuff)
                      (let (((slots . stuff) stuff))
                         (map (lambda (slot) (eval (list define-slot slot) env)) slots)
                         (unless (null? stuff)
                            (let (((_ ctor-name args) (car stuff)))
                               (eval (list define-constructor ctor-name name args) env))))))))
         name))

   (define define-interface
      (vau (name) env
         (eval (list define-class name ()) env)))

   (define define-implementation add-superclass!)

   (define define-constructor
      (vau (name class slots) env
         (eval (list define name
                  (list lambda slots
                     (list construct-with-slots class slots)))
               env)))

   (define construct-with-slots
      (vau (class slots) env
         (let ((obj (make-instance (eval class env))))
            (map (lambda (slot)
                    (set-slot! obj (to-string slot) (eval slot env)))
                 slots)
            obj)))

   (define define-slot
      (vau (name) env
         (let* ((slot (to-string name))
                (generic-name (intern (strcat "." slot)))
                (reader (lambda (obj) (get-slot obj slot)))
                (writer (lambda (val obj) (set-slot! obj slot val))))
            (set-setter! reader writer)
            (eval (list define generic-name reader) env))
         name))

   (define define-generic
      (vau (name . _) env
         (eval (list define name
                  (lambda (self . otree) (send self (to-string name) otree)))
               env)
         name))

   (define define-method
      (vau (name ((rcv class) . rest) . body) env
         (put-method! (eval class env)
                      (to-string name)
                      (eval (list* lambda (list* rcv rest) body) env))
         name)))

;;;; Property Lists

;; Calls fun with name and value of each property in plist.
(define map-plist
   (lambda (fun plist)
      (if (null? plist)
          ()
          (cons (fun (car plist) (cadr plist))
                (map-plist fun (cddr plist))))))

;;;; Options

(define-interface Option)

(define-class None (Option))

(define none (make-instance None))

(define-class Some (Option)
   (value)
   (:constructor option (value)))

(define-generic option-supplied? (option))
(define-method option-supplied? ((n None)) #f)
(define-method option-supplied? ((s Some)) #t)

(define if-option
   (vau ((name option) then . else) env
      (let ((o (eval option env)))
         (if (option-supplied? o)
             (eval (list let (list (list name (.value o))) then) env)
             (unless (null? else)
                (eval (car else) env))))))

;;;; Collections

(provide (Iterable
          Iterator
          iterator
          has-next?
          next!
          at)

   (define-interface Iterable)
   (define-interface Iterator)
   (define-generic iterator)
   (define-generic has-next?)
   (define-generic next!)
   (define-generic at)

   (define-method at ((s String) (i Number))
      (strelt s i))

)

;;;; Condition System

(provide (Condition
          Error
          Warning
          Restart
          Unhandled-Restart-Error
          call-with-handler
          signal
          default-handler)

   (define-class Condition)
   (define-class Error (Condition))
   (define-class Warning (Condition))

   (define-class Restart (Condition))
   (define-class Unhandled-Restart-Error (Error)
      (restart)
      (:constructor unhandled-restart-error (restart)))


   (define *handler-stack* none)

   (define-class Handler-Frame ()
      (parent-option
       matcher
       handler)
      (:constructor make-handler-frame (parent-option matcher handler)))

   (define call-with-handler
      (lambda (matcher handler thunk)
         (let ((saved-stack *handler-stack*))
            (set! *handler-stack* (option (make-handler-frame *handler-stack*
                                                              matcher
                                                              handler)))
            (unwind-protect (thunk)
               (set! *handler-stack* saved-stack)))))

   (define signal
      (lambda (condition)
         (define do-signal
            (lambda (frame-option)
               (if-option (frame frame-option)
                  (if ((.matcher frame) condition)
                      ((.handler frame) condition)
                      (do-signal (.parent-option frame)))
                  (default-handler condition))))
         (do-signal *handler-stack*)))


   (define-generic default-handler (condition))

   (define-method default-handler ((c Condition))
      #void)

   (define-method default-handler ((w Warning))
      (print w))

   (define-method default-handler ((e Error))
      (invoke-debugger e))

   (define-method default-handler ((r Restart))
      (signal (unhandled-restart-error r)))

)

;; Permission is hereby granted, free of charge, to any person
;; obtaining a copy of this software and associated documentation
;; files (the "Software"), to deal in the Software without
;; restriction, including without limitation the rights to use, copy,
;; modify, merge, publish, distribute, sublicense, and/or sell copies
;; of the Software, and to permit persons to whom the Software is
;; furnished to do so, subject to the following conditions:
;;
;; The above copyright notice and this permission notice shall be
;; included in all copies or substantial portions of the Software.
;;
;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
;; EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
;; MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
;; NONINFRINGEMENT. ALSO, THERE IS NO KERNEL UNDERGROUND; IT'S ALL
;; JUST RUMOUR AND HEARSAY. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
;; HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
;; WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
;; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.
