;; Virtua standard language.                      -*- Scheme -*-
;; Copyright (c) 2012 Manuel Simoni. See license at end of file.

($define! define $define!)
(define if $if)
(define #void #inert)
(define _ #ignore)
(define vau $vau)
(define catch $catch)
(define loop $loop)

(define ' (vau (form) _ form))

;;;; These are adapted from John Shutt's SINK and the R-1RK.

(define begin
   ((wrap (vau (seq2) _
             (seq2
                (define aux
                   (vau (head . tail) env
                      (if (null? tail)
                          (eval head env)
                          (seq2
                             (eval head env)
                             (eval (cons aux tail) env)))))
                (vau body env
                   (if (null? body)
                       #void
                       (eval (cons aux body) env))))))
      (vau (first second) env
         ((wrap (vau _ _ (eval second env)))
          (eval first env)))))

(define list (wrap (vau x _ x)))

(define list*
   (wrap (vau args _
            (begin
               (define aux
                  (wrap (vau ((head . tail)) _
                           (if (null? tail)
                               head
                               (cons head (aux tail))))))
               (aux args)))))

(define vau
   ((wrap (vau (vau) _
             (vau (formals eformal . body) env
                (eval (list vau formals eformal
                            (cons begin body))
                      env))))
      vau))

(define lambda
   (vau (formals . body) env
      (wrap (eval (list* vau formals _ body)
                  env))))

(define caar (lambda (((x . _) . _)) x))
(define cdar (lambda (((_ . x) . _)) x))
(define cadr (lambda ((_ . (x . _))) x))
(define cddr (lambda ((_ . (_ . x))) x))

(define apply
   (lambda (appv arg . opt)
      (eval (cons (unwrap appv) arg)
            (if (null? opt)
                (make-environment)
                (car opt)))))

(define or?
   (lambda (a b) (if a #t (if b #t #f))))

(define map
   (lambda (fn list)
      (if (null? list)
          ()
          (cons (fn (car list))
                (map fn (cdr list))))))

(define let
   (vau (bindings . body) env
      (eval (cons (list* lambda (map car bindings) body)
                  (map cadr bindings))
            env)))

(define let*
   (vau (bindings . body) env
      (eval (if (null? bindings)
                (list* let bindings body)
                (list let
                      (list (car bindings))
                      (list* let* (cdr bindings) body)))
            env)))

(define cond
   (vau clauses env
      (define aux
         (lambda ((test . body) . clauses)
            (if (eval test env)
                (apply (wrap begin) body env)
                (apply (wrap cond) clauses env))))
      (if (null? clauses)
          #void
          (apply aux clauses))))

(define not? (lambda (x) (if x #f #t)))

(define get-current-environment (wrap (vau () e e)))

(define provide
   (vau (symbols . body) env
      (eval (list define symbols
               (list let ()
                  (list* begin body)
                  (list* list symbols)))
            env)))

;;;; Virtua-specific forms

(define assert
   (vau (that) env
      (if (not? (eval that env))
          (error (to-string that))
          #void)))

(define block
   (vau (name . body) env
      ((eval (list lambda (list name)
                (list* catch name body))
             env)
       (cons () ()))))

(define return-from
   (lambda (name value)
      (throw name value)))

(define unwind-protect
   (vau (protected . cleanup) env
      (eval (list $unwind-protect protected (list* begin cleanup)) env)))

(define scope
   (vau body env
      (eval (list* let () body) env)))

(define set!
   (vau ((cmb . args) value) env
      (eval (list* (setter (eval cmb env)) value args) env)))

(define setter (lambda (obj) (get-slot obj "setter")))
(define set-setter! (lambda (obj setter) (set-slot! obj "setter" setter)))
(set-setter! setter set-setter!)

;;;; Object System

(provide (define-class
          define-interface
          define-implementation
          define-constructor
          define-slot
          define-generic
          define-method)

   (define define-class
      (vau (name superclasses) env
         (eval (list define name
                  (list make-class (list* list superclasses)))
               env)
         name))

   (define define-interface
      (vau (name) env
         (eval (list define-class name ()) env)))

   (define define-implementation add-superclass!)

   (define define-constructor
      (vau (name class slots) env
         (eval (list define name
                  (list lambda slots
                     (list construct-with-slots class slots)))
               env)))

   (define construct-with-slots
      (vau (class slots) env
         (let ((obj (make-instance (eval class env))))
            (map (lambda (slot)
                    (set-slot! obj (to-string slot) (eval slot env)))
                 slots)
            obj)))

   (define define-slot
      (vau (name) env
         (let* ((slot (to-string name))
                (generic-name (intern (strcat "." slot)))
                (reader (lambda (obj) (get-slot obj slot)))
                (writer (lambda (obj val) (set-slot! obj slot val))))
            (set! (setter reader) writer)
            (eval (list define generic-name reader) env))
         name))

   (define define-generic
      (vau (name . _) env
         (eval (list define name
                  (lambda (self . otree) (send self (to-string name) otree)))
               env)
         name))

   (define define-method
      (vau (name ((rcv class) . rest) . body) env
         (put-method! (eval class env)
                      (to-string name)
                      (eval (list* lambda (list* rcv rest) body) env))
         name)))

;;;; Collections

(provide (Iterable
          Iterator
          iterator
          has-next?
          next!)

   (define-interface Iterable)
   (define-interface Iterator)
   (define-generic iterator)
   (define-generic has-next?)
   (define-generic next!)

   (scope
      (define-implementation Pair Iterable)
      (define-method iterator ((p Pair))
         (make-pair-iterator p))

      (define-implementation Nil Iterable)
      (define-method iterator ((n Nil))
         (make-pair-iterator n))

      (define-class Pair-Iterator ())
      (define-slot current)
      (define-constructor make-pair-iterator Pair-Iterator (current))

      (define-method has-next? ((it Pair-Iterator))
         (null? (.current it)))
      (define-method next! ((it Pair-Iterator))
         (let ((obj (car (get-slot it "current"))))
            (set! (.current it) (cdr (.current it))))))

)

;; Permission is hereby granted, free of charge, to any person
;; obtaining a copy of this software and associated documentation
;; files (the "Software"), to deal in the Software without
;; restriction, including without limitation the rights to use, copy,
;; modify, merge, publish, distribute, sublicense, and/or sell copies
;; of the Software, and to permit persons to whom the Software is
;; furnished to do so, subject to the following conditions:
;;
;; The above copyright notice and this permission notice shall be
;; included in all copies or substantial portions of the Software.
;;
;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
;; EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
;; MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
;; NONINFRINGEMENT. ALSO, THERE IS NO KERNEL UNDERGROUND; IT'S ALL
;; JUST RUMOUR AND HEARSAY. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
;; HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
;; WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
;; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.
